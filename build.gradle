plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' 
}

group = 'kaiakk'
version = '1.00.0'
def targetJavaVersion = 8

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/service/rest/repository/browse/snapshots/org/spigotmc/spigot-api/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    bukkit {
        java {
            srcDirs = ['bukkit/main/java']
        }
        resources {
            srcDirs = ['bukkit/main/resources']
        }
    }

    folia {
        java {
            srcDirs = ['folia/main/java']
        }
        resources {
            srcDirs = ['folia/main/resources']
        }
    }

    bungeeWaterfall {
        java {
            srcDirs = ['bungeecord-waterfall/main/java']
        }
        resources {
            srcDirs = ['bungeecord-waterfall/main/resources']
        }
    }

    velocity {
        java {
            srcDirs = ['velocity/main/java']
        }
        resources {
            srcDirs = ['velocity/main/templates', 'velocity/main/resources']
        }
    }
}

configurations {
    bukkitCompileOnly.extendsFrom compileOnly
    bukkitImplementation.extendsFrom implementation
    bukkitRuntimeClasspath.extendsFrom runtimeClasspath

    foliaCompileOnly.extendsFrom compileOnly
    foliaImplementation.extendsFrom implementation
    foliaRuntimeClasspath.extendsFrom runtimeClasspath

    bungeeWaterfallCompileOnly.extendsFrom compileOnly
    bungeeWaterfallImplementation.extendsFrom implementation
    bungeeWaterfallRuntimeClasspath.extendsFrom runtimeClasspath

    velocityCompileOnly.extendsFrom compileOnly
    velocityAnnotationProcessor.extendsFrom annotationProcessor
    velocityImplementation.extendsFrom implementation
    velocityRuntimeClasspath.extendsFrom runtimeClasspath
}

dependencies {
    add('bukkitCompileOnly', 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT')

    add('foliaCompileOnly', 'dev.folia:folia-api:1.20.1-R0.1-SNAPSHOT')
    add('foliaCompileOnly', 'net.kyori:adventure-api:4.14.0')
    add('foliaCompileOnly', 'net.kyori:adventure-platform-bukkit:4.2.0')

    add('bungeeWaterfallCompileOnly', 'net.md-5:bungeecord-api:1.16-R0.3') {
        exclude group: 'net.md-5', module: 'brigadier'
    }
    
    add('velocityCompileOnly', 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT')
    add('velocityAnnotationProcessor', 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT')
    add('velocityImplementation', 'org.yaml:snakeyaml:2.0')
}

java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (name.contains('Folia')) {
        options.release.set(17)
    } else {
        options.release.set(targetJavaVersion)
    }
}

tasks.named('compileVelocityJava', JavaCompile) {
    options.release.set(17)
}

tasks.named('processBukkitResources', ProcessResources) {
    expand(version: version)
    filteringCharset = 'UTF-8'
}

tasks.named('processFoliaResources', ProcessResources) {
    expand(version: version)
    filteringCharset = 'UTF-8'
}

tasks.named('processBungeeWaterfallResources', ProcessResources) {
    expand(version: version)
    filteringCharset = 'UTF-8'
}

tasks.named('processVelocityResources', ProcessResources) {
    expand(version: version)
    filteringCharset = 'UTF-8'
}

def baseName = 'Axior'

tasks.register('shadowBukkitJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn 'compileBukkitJava', 'processBukkitResources'
    archiveBaseName.set("${baseName}-Bukkit")
    archiveClassifier.set('')
    archiveVersion.set(version)
    from sourceSets.bukkit.output
    configurations = [project.configurations.bukkitRuntimeClasspath]
    mergeServiceFiles()
}

tasks.register('shadowFoliaJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn 'compileFoliaJava', 'processFoliaResources'
    archiveBaseName.set("${baseName}-Folia")
    archiveClassifier.set('')
    archiveVersion.set(version)
    from sourceSets.folia.output
    configurations = [project.configurations.foliaRuntimeClasspath]
    mergeServiceFiles()
}

tasks.register('shadowBungeeJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn 'compileBungeeWaterfallJava', 'processBungeeWaterfallResources'
    archiveBaseName.set("${baseName}-BungeeCord")
    archiveClassifier.set('')
    archiveVersion.set(version)
    from sourceSets.bungeeWaterfall.output
    configurations = [project.configurations.bungeeWaterfallRuntimeClasspath]
    mergeServiceFiles()
}

tasks.register('shadowVelocityJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn 'compileVelocityJava', 'processVelocityResources'
    archiveBaseName.set("${baseName}-Velocity")
    archiveClassifier.set('')
    archiveVersion.set(version)
    from sourceSets.velocity.output
    configurations = [project.configurations.velocityRuntimeClasspath]
    relocate 'org.yaml.snakeyaml', 'kaiakk.axior.libs.snakeyaml'
    mergeServiceFiles()
}

tasks.build {
    dependsOn shadowBukkitJar, shadowFoliaJar, shadowBungeeJar, shadowVelocityJar
}

jar.enabled = false
tasks.named('shadowJar').configure {
    enabled = false
}