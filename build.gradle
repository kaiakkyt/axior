plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' 
}

group = 'kaiakk'
version = '1.01.1'
def baseName = 'Axior'

repositories {
    mavenCentral()
    maven { url = "hub.spigotmc.org" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets {
    bukkit {
        java.srcDirs = ['bukkit-folia/main/java']
        resources.srcDirs = ['bukkit-folia/main/resources']
    }
    folia {
        java.srcDirs = ['bukkit-folia/main/java']
        resources.srcDirs = ['bukkit-folia/main/resources']
    }
    bungeeWaterfall {
        java.srcDirs = ['bungeecord-waterfall/main/java']
        resources.srcDirs = ['bungeecord-waterfall/main/resources']
    }
    velocity {
        java.srcDirs = ['velocity/main/java']
        resources.srcDirs = ['velocity/main/templates', 'velocity/main/resources']
    }
}

configurations {
    bukkitCompileOnly.extendsFrom compileOnly
    bukkitImplementation.extendsFrom implementation
    bukkitRuntimeClasspath.extendsFrom runtimeClasspath

    foliaCompileOnly.extendsFrom compileOnly
    foliaImplementation.extendsFrom implementation
    foliaRuntimeClasspath.extendsFrom runtimeClasspath

    bungeeWaterfallCompileOnly.extendsFrom compileOnly
    bungeeWaterfallImplementation.extendsFrom implementation
    bungeeWaterfallRuntimeClasspath.extendsFrom runtimeClasspath

    velocityCompileOnly.extendsFrom compileOnly
    velocityAnnotationProcessor.extendsFrom annotationProcessor
    velocityImplementation.extendsFrom implementation
    velocityRuntimeClasspath.extendsFrom runtimeClasspath
}

dependencies {
    add('bukkitCompileOnly', 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT')
    add('bukkitImplementation', files('dependencies/Multimedia-1.10.1.jar'))
    
    add('foliaCompileOnly', 'dev.folia:folia-api:1.20.1-R0.1-SNAPSHOT')
    add('foliaCompileOnly', 'net.kyori:adventure-api:4.14.0')
    add('foliaCompileOnly', 'net.kyori:adventure-platform-bukkit:4.2.0')
    add('foliaImplementation', files('dependencies/Multimedia-1.10.1.jar'))

    add('bungeeWaterfallCompileOnly', 'net.md-5:bungeecord-api:1.16-R0.3') {
        exclude group: 'net.md-5', module: 'brigadier'
    }
    
    add('velocityCompileOnly', 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT')
    add('velocityAnnotationProcessor', 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT')
    add('velocityImplementation', 'org.yaml:snakeyaml:2.0')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (name.contains('Folia') || name.contains('Velocity')) {
        options.release.set(17)
    } else {
        options.release.set(8)
    }
}

[processBukkitResources, processFoliaResources, processBungeeWaterfallResources, processVelocityResources].each { task ->
    task.configure {
        expand(version: version)
        filteringCharset = 'UTF-8'
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

tasks.register('shadowBukkitJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    from sourceSets.bukkit.output
    configurations = [project.configurations.bukkitRuntimeClasspath]
    archiveBaseName.set("${baseName}-Bukkit-Folia")
    archiveClassifier.set('')
    archiveVersion.set(version)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    mergeServiceFiles()
}

tasks.register('shadowBungeeJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    from sourceSets.bungeeWaterfall.output
    configurations = [project.configurations.bungeeWaterfallRuntimeClasspath]
    archiveBaseName.set("${baseName}-Bungee")
    archiveClassifier.set('')
    archiveVersion.set(version)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    mergeServiceFiles()
}

tasks.register('shadowVelocityJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    from sourceSets.velocity.output
    configurations = [project.configurations.velocityRuntimeClasspath]
    archiveBaseName.set("${baseName}-Velocity")
    archiveClassifier.set('')
    archiveVersion.set(version)
    relocate 'org.yaml.snakeyaml', 'kaiakk.axior.libs.snakeyaml'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    mergeServiceFiles()
}

tasks.build {
    dependsOn shadowBukkitJar, shadowBungeeJar, shadowVelocityJar
}

jar.enabled = false
shadowJar.enabled = false
